{# NOTE: This file is rendered to communication.zeek during "zkg install" -#}
{# -#}
module eZeeKonfigurator;

@load ./options

export {

    ## The brokerd port we connect to
    const brokerd_port: port = {{ brokerd_port }}/tcp &redef;

    ## The brokerd port we connect to
    const brokerd_address: addr = {{ brokerd_address }} &redef;

    ## Our unique identifier
    const uuid: string = "{{ uuid }}" &redef;

    ## URL of our eZeeKonfigurator webserver
    const url: string = "{{ url }}" &redef;

}

function start_brokerd()
{
    setenv("BROKERD_BIND_PORT", cat(port_to_count(brokerd_port)));
    setenv("UUID", uuid);
    setenv("URL", url);
    system(fmt("%s/%s %s/%s", @DIR, "venv/bin/python", @DIR, "venv/bin/run_brokerd.py"));
}

event zeek_init()
{
    start_brokerd();
    Broker::peer(cat(brokerd_address), brokerd_port);
}

event Broker::peer_added(endpoint: Broker::EndpointInfo, msg: string) &priority=10
{
    if ( endpoint?$network && to_addr(endpoint$network$address) == brokerd_address && endpoint$network$bound_port == brokerd_port )
        {
        local topic = cat("/ezeekonfigurator/control/", uuid);

        Broker::subscribe(topic);
        Broker::auto_publish(topic, heartbeat);
        Broker::auto_publish(topic, last_gasp);
        Broker::auto_publish(topic, option_change_reply);
        Broker::auto_publish(topic, sensor_info_reply);
        Broker::auto_publish(topic, option_list_reply);
            
        event eZeeKonfigurator::heartbeat(getpid());
        event eZeeKonfigurator::sensor_info_reply(SensorInfoMessage());
        }
}

event Broker::peer_lost(endpoint: Broker::EndpointInfo, msg: string) &priority=10
{
    if ( endpoint?$network && to_addr(endpoint$network$address) == brokerd_address && endpoint$network$bound_port == brokerd_port )
        {
        start_brokerd();
        }
}

event net_done(t: time)
{
    event eZeeKonfigurator::last_gasp("net_done");
}

event zeek_done()
{
    event eZeeKonfigurator::last_gasp("zeek_done");
}

